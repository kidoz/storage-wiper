# Maintainer: Your Name <your.email@example.com>

pkgname=storage-wiper-git
_pkgname=storage-wiper
pkgver=1.0.0.r6.31bf288
pkgrel=1
pkgdesc='Secure disk wiping tool with GTK4/libadwaita interface and DoD-compliant algorithms (git version)'
arch=('x86_64')
url='https://github.com/kidoz/storage-wiper'
license=('MIT')
depends=(
  'gtk4'
  'gtkmm-4.0'
  'libadwaita'
  'polkit'
)
makedepends=(
  'meson'
  'ninja'
  'git'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/kidoz/storage-wiper.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  # Format: {base_version}.r{commits}.{short_hash}
  local base_ver="1.1.0"
  local commits=$(git rev-list --count HEAD 2>/dev/null || echo 0)
  local hash=$(git rev-parse --short=7 HEAD 2>/dev/null || echo unknown)
  printf "%s.r%s.%s" "$base_ver" "$commits" "$hash"
}

build() {
  arch-meson "$_pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Install license
  install -Dm644 "$_pkgname/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
